let indent spaces string =
  let indent_string = String.make spaces ' ' in
  String.split_on_char '\n' string
  |> List.concat_map (fun line ->
    if String.equal line "" then [ "\n" ] else [ indent_string; line; "\n" ])
  |> String.concat ""

let template ~tests = {|(* TEST
 flambda2;
 include stdlib_upstream_compatible;
 include stdlib_stable;
 {
   native;
 }{
   flags = "-O3";
   native;
 }{
   bytecode;
 }{
   flags = "-extension layouts_alpha";
   native;
 }{
   flags = "-extension layouts_alpha -O3";
   native;
 }{
   flags = "-extension layouts_alpha";
   bytecode;
 }{
   flags = "-extension layouts_beta";
   native;
 }{
   flags = "-extension layouts_beta -O3";
   native;
 }{
   flags = "-extension layouts_beta";
   bytecode;
 }
*)

(* Generated by [generate_stringlike_indexing.ml] *)

let lengths = List.init 17 (fun x -> x) @ List.init 17 (fun x -> 300 + x)

type exn += Test_failed

let create_s length =
  String.init length (fun i -> i * 7 mod 256 |> char_of_int)
;;

let create_b length = create_s length |> Bytes.of_string

open struct
  open Bigarray

  type bigstring = (char, int8_unsigned_elt, c_layout) Array1.t

  let bigstring_of_string s =
    let a = Array1.create char c_layout (String.length s) in
    for i = 0 to String.length s - 1 do
      a.{i} <- s.[i]
    done;
    a

  let create_bs length = create_s length |> bigstring_of_string
end

module Tester (Primitives : sig
    type boxed_index
    type boxed_data
    type container

    val create : int -> container
    val generate_data : int -> boxed_data
    val to_index : int -> boxed_index
    val data_equal : boxed_data -> boxed_data -> bool

    type 'a getter := container -> 'a -> boxed_data
    type 'a setter := container -> 'a -> boxed_data -> unit

    val get_reference : int getter
    val get_safe : boxed_index getter
    val get_unsafe : boxed_index getter
    val set_reference : int setter
    val set_safe : boxed_index setter
    val set_unsafe : boxed_index setter
    val extra_bounds_checks : boxed_index list
    val max_index : int
  end) : sig end = struct
  open Primitives

  let make_tester_functions length =
    let for_reference = create length
    and for_safe = create length
    and for_unsafe = create length in
    let check_get_bounds i =
      try
        let _ = get_safe for_safe i in
        assert false
      with
      | Invalid_argument _ -> ()
    in
    let check_set_bounds i x =
      try
        let _ = set_safe for_safe i x in
        assert false
      with
      | Invalid_argument _ -> ()
    in
    let check_get i =
      let test_i = to_index i in
      try
        let res = get_reference for_reference i in
        try
          assert (data_equal res (get_safe for_safe test_i));
          assert (data_equal res (get_unsafe for_unsafe test_i))
        with
        | _ -> raise Test_failed
      with
      | Test_failed -> assert false
      | Invalid_argument _ -> check_get_bounds test_i
      | _ ->
        (try
           let _ = get_safe for_safe test_i in
           assert false
         with
         | Invalid_argument _ -> assert false
         | _ -> ())
    in
    let check_set i x =
      let test_i = to_index i in
      try
        set_reference for_reference i x;
        try
          set_safe for_safe test_i x;
          assert (data_equal x (get_reference for_safe i));
          set_unsafe for_unsafe test_i x;
          assert (data_equal x (get_reference for_unsafe i));
          (* Check that we didn't ruin adjacent indices *)
          check_get (i - 1);
          check_get (i + 1)
        with
        | _ -> raise Test_failed
      with
      | Test_failed -> assert false
      | Invalid_argument _ -> check_set_bounds test_i x
      | _ ->
        (try
           set_safe for_safe test_i x;
           assert false
         with
         | Invalid_argument _ -> assert false
         | _ -> ())
    in
    check_get_bounds, check_get, check_set_bounds, check_set
  ;;

  let test length =
    Random.init 1234;
    let check_get_bounds, check_get, check_set_bounds, check_set =
      make_tester_functions length
    in
    for i = -1 to min max_index (length + 1) do
      check_get i;
      check_set i (generate_data i)
    done;
    List.iter
      (fun bound ->
        check_get_bounds bound;
        check_set_bounds bound (generate_data 1))
      extra_bounds_checks
  ;;

  let () = List.iter test lengths
end

module Tester_no_set (Primitives : sig
    type boxed_index
    type boxed_data
    type container

    val create : int -> container
    val generate_data : int -> boxed_data
    val to_index : int -> boxed_index
    val data_equal : boxed_data -> boxed_data -> bool

    type 'a getter := container -> 'a -> boxed_data

    val get_reference : int getter
    val get_safe : boxed_index getter
    val get_unsafe : boxed_index getter
    val extra_bounds_checks : boxed_index list
    val max_index : int
  end) : sig end = struct
  open Primitives

  let make_tester_functions length =
    let for_reference = create length
    and for_safe = create length
    and for_unsafe = create length in
    let check_get_bounds i =
      try
        let _ = get_safe for_safe i in
        assert false
      with
      | Invalid_argument _ -> ()
    in
    let check_get i =
      let test_i = to_index i in
      try
        let res = get_reference for_reference i in
        try
          assert (data_equal res (get_safe for_safe test_i));
          assert (data_equal res (get_unsafe for_unsafe test_i))
        with
        | _ -> raise Test_failed
      with
      | Test_failed -> assert false
      | Invalid_argument _ -> check_get_bounds test_i
      | _ ->
        (try
           let _ = get_safe for_safe test_i in
           assert false
         with
         | Invalid_argument _ -> assert false
         | _ -> ())
    in
    check_get_bounds, check_get
  ;;

  let test length =
    Random.init 1234;
    let check_get_bounds, check_get = make_tester_functions length in
    for i = -1 to min max_index (length + 1) do
      check_get i;
    done;
    List.iter (fun bound -> check_get_bounds bound) extra_bounds_checks
  ;;

  let () = List.iter test lengths
end

module Nativeint_u = Stdlib_upstream_compatible.Nativeint_u
module Int64_u = Stdlib_upstream_compatible.Int64_u
module Int32_u = Stdlib_upstream_compatible.Int32_u
module Int16 = Stdlib_stable.Int16
module Int16_u = Stdlib_stable.Int16_u
module Int8 = Stdlib_stable.Int8
module Int8_u = Stdlib_stable.Int8_u
module Float32 = Stdlib_stable.Float32
module Float32_u = Stdlib_stable.Float32_u

external int64_of_int64 : int64 -> int64 = "%identity"
external int64_to_int64 : int64 -> int64 = "%identity"
external int32_of_int64 : int64 -> int32 = "%int32_of_int64"
external int32_to_int64 : int32 -> int64 = "%int64_of_int32"
external int16_of_int64 : int64 -> int16 = "%int16_of_int64"
external int16_to_int64 : int16 -> int64 = "%int64_of_int16"
external int8_of_int64 : int64 -> int8 = "%int8_of_int64"
external int8_to_int64 : int8 -> int64 = "%int64_of_int8"
external int_of_int64 : int64 -> int = "%int_of_int64"
external int_to_int64 : int -> int64 = "%int64_of_int"
external int_of_int : int -> int = "%identity"
external int_to_int : int -> int = "%identity"

|}^tests

let test_group_template ~setup ~tests = {|
open struct
|}^indent 2 setup^{|
|}^indent 2 (String.concat "" tests)^{|
end
|}

let type_utilities_template
  ~boxed_index
  ~boxed_data
  ~generate_data
  ~to_index
  ~unbox_index
  ~unbox_data
  ~box_data
  ~data_equal
  ~extra_bounds
  ~max_index
  = {|
type boxed_index = |}^boxed_index^{|
type boxed_data = |}^boxed_data^{|

let generate_data = |}^generate_data^{|
let to_index = |}^to_index^{|
let data_equal = |}^data_equal^{|
let unbox_index = |}^unbox_index^{|
let unbox_data = |}^unbox_data^{|
let box_data = |}^box_data^{|
let extra_bounds_checks = |}^extra_bounds^{|
let max_index = |}^max_index

let one_test_template
  ~test_setters
  ~index
  ~boxed_data
  ~tested_data
  ~index_sigil
  ~get_sigil
  ~set_sigil
  ~unboxed_sigil
  ~container
  ~create
  = {|
module _ = |}^(if test_setters then "Tester" else "Tester_no_set")^{| (struct
    type nonrec boxed_index = boxed_index
    type nonrec boxed_data = boxed_data
    type container = |}^container^{|

    let create = |}^create^{|
    let generate_data = generate_data
    let to_index = to_index
    let data_equal = data_equal
    let extra_bounds_checks = extra_bounds_checks
    let max_index = max_index

    external get_reference
      : |}^container^{|
      -> int
      -> |}^boxed_data^{|
      = "%caml_|}^container^{|_get|}^get_sigil^{|"

    external get_safe
      :  |}^container^{|
      -> |}^index^{|
      -> |}^tested_data^{|
      = "%caml_|}^container^{|_get|}^get_sigil^unboxed_sigil^index_sigil^{|"

    let get_safe b i = box_data (get_safe b (unbox_index i))

    external get_unsafe
      :  |}^container^{|
      -> |}^index^{|
      -> |}^tested_data^{|
      = "%caml_|}^container^{|_get|}^get_sigil^{|u|}^unboxed_sigil^index_sigil^{|"

    let get_unsafe b i = box_data (get_unsafe b (unbox_index i))
|}^(if test_setters then {|
    external set_reference
      : |}^container^{|
      -> int
      -> |}^boxed_data^{|
      -> unit
      = "%caml_|}^container^{|_set|}^set_sigil^{|"

    external set_safe
      :  |}^container^{|
      -> |}^index^{|
      -> |}^tested_data^{|
      -> unit
      = "%caml_|}^container^{|_set|}^set_sigil^unboxed_sigil^index_sigil^{|"

    let set_safe b i d = set_safe b (unbox_index i) (unbox_data d)

    external set_unsafe
      :  |}^container^{|
      -> |}^index^{|
      -> |}^tested_data^{|
      -> unit
      = "%caml_|}^container^{|_set|}^set_sigil^{|u|}^unboxed_sigil^index_sigil^{|"

    let set_unsafe b i d = set_unsafe b (unbox_index i) (unbox_data d)|} else "")^{|
  end)
|}

let int_examples_template
      ~module_ ~min ~max ~min_for_rand ~max_for_rand ~type_ ~type_for_rand = {|
fun i ->
  match i mod 4 with
  | 0 -> |}^module_^{|.zero
  | 1 -> |}^min^{|
  | 2 -> |}^max^{|
  | _ -> Random.|}^type_for_rand^{|_in_range
             ~min:|}^min_for_rand^{|
             ~max:|}^max_for_rand^{|
           |> |}^type_^{|_of_|}^type_for_rand^{|
|}

let int_examples_custom_bounds ~module_ ~width ~type_ =
  let min_int = {|(|}^module_^{|.(shift_left one) (|}^width^{| - 1))|} in
  let max_int = {|
    (let shift = |}^width^{| - 1 in
    |}^module_^{|.(lognot (shift_left (shift_right (lognot zero) shift) shift)))|}
  in
  int_examples_template
    ~module_
    ~min:min_int
    ~max:max_int
    ~min_for_rand:(module_^{|.zero|})
    ~max_for_rand:min_int
    ~type_
    ~type_for_rand:type_

let int_examples_default_bounds ~module_ ~width ~type_ =
  let min = module_^{|.min_int|} in
  let max = module_^{|.max_int|} in
  let min_for_rand = {|(|}^type_^{|_to_int64 |}^min^{|)|} in
  let max_for_rand = {|(|}^type_^{|_to_int64 |}^max^{|)|} in
  int_examples_template
    ~module_ ~min ~max ~min_for_rand ~max_for_rand ~type_ ~type_for_rand:"int64"

let float_examples = {|
fun _ ->
  let f =
    let f = Random.float Float.max_float in
    if Random.bool () then Float.neg f else f
  in
  Float32.of_float f
|}

let int_extra_bounds_template ~module_ =
  module_ ^ {|.[ min_int; max_int; add min_int one; sub zero one ]|}
;;

let small_int_extra_bounds_template ~module_ =
  module_ ^ {|.[ min_int; add min_int one; sub zero one ]|}
;;

type index =
  { boxed_type : string
  ; tested_type : string
  ; of_int : string
  ; unbox : string
  ; extra_bounds : string
  ; max_index : string
  }

type result =
  { boxed_type : string
  ; tested_type : string
  ; width : string
  ; unboxed_sigil : string
  ; box : string
  ; unbox : string
  ; eq : string
  ; example : string
  }

type container =
  { container : string
  ; create : string
  ; test_setters : bool
  }

let int_result ~width ~unboxed ~module_ ~examples =
  let type_ = String.lowercase_ascii module_ in
  let unboxed_sigil = if unboxed then "#" else "" in
  { boxed_type = type_
  ; tested_type = type_ ^ unboxed_sigil
  ; width
  ; unboxed_sigil
  ; box =
      (if unboxed
       then module_ ^ "_u.to_" ^ type_
       else "fun x -> x")
  ; unbox =
      (if unboxed
       then module_ ^ "_u.of_" ^ type_
       else "fun x -> x")
  ; eq = module_ ^ ".equal"
  ; example = examples ~module_ ~width ~type_ |> indent 2
  }
;;

let indices =
  [ { boxed_type = "nativeint"
    ; tested_type = "nativeint#"
    ; of_int = "Nativeint.of_int"
    ; unbox = "Nativeint_u.of_nativeint"
    ; extra_bounds = int_extra_bounds_template ~module_:"Nativeint"
    ; max_index = "Int.max_int - 1"
    }
  ; { boxed_type = "int8"
    ; tested_type = "int8#"
    ; of_int = "Int8.of_int"
    ; unbox = "Int8_u.of_int8"
    ; extra_bounds = small_int_extra_bounds_template ~module_:"Int8"
    ; max_index = "Int8.(to_int max_int) - 1"
    }
  ; { boxed_type = "int16"
    ; tested_type = "int16#"
    ; of_int = "Int16.of_int"
    ; unbox = "Int16_u.of_int16"
    ; extra_bounds = int_extra_bounds_template ~module_:"Int16"
    ; max_index = "Int16.(to_int max_int) - 1"
    }
  ; { boxed_type = "int32"
    ; tested_type = "int32#"
    ; of_int = "Int32.of_int"
    ; unbox = "Int32_u.of_int32"
    ; extra_bounds = int_extra_bounds_template ~module_:"Int32"
    ; max_index = "Int32.(to_int max_int) - 1"
    }
  ; { boxed_type = "int64"
    ; tested_type = "int64#"
    ; of_int = "Int64.of_int"
    ; unbox = "Int64_u.of_int64"
    ; extra_bounds = int_extra_bounds_template ~module_:"Int64"
    ; max_index = "Int.max_int - 1"
    }
  ]
;;

let datas =
  [ int_result ~width:"i8" ~unboxed:false ~module_:"Int8"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"16" ~unboxed:false ~module_:"Int"
      ~examples:int_examples_custom_bounds
  ; int_result ~width:"i16" ~unboxed:false ~module_:"Int16"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"32" ~unboxed:false ~module_:"Int32"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"64" ~unboxed:false ~module_:"Int64"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"i8" ~unboxed:true ~module_:"Int8"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"i16" ~unboxed:true ~module_:"Int16"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"32" ~unboxed:true ~module_:"Int32"
      ~examples:int_examples_default_bounds
  ; int_result ~width:"64" ~unboxed:true ~module_:"Int64"
      ~examples:int_examples_default_bounds
  ; {
      width = "f32";
      unboxed_sigil = "";
      boxed_type = "float32";
      tested_type = "float32";
      box = "fun x -> x";
      unbox = "fun x -> x";
      eq = "Float32.equal";
      example = float_examples;
    }
  ; {
      width = "f32";
      unboxed_sigil = "#";
      boxed_type = "float32";
      tested_type = "float32#";
      box = "Float32_u.to_float32";
      unbox = "Float32_u.of_float32";
      eq = "Float32.equal";
      example = float_examples;
    };
  ]

let containers =
  [ { container = "string"; create = "create_s"; test_setters = false }
  ; { container = "bytes"; create = "create_b"; test_setters = true }
  ; { container = "bigstring"; create = "create_bs"; test_setters = true }
  ]
;;

let tests =
  let ( let* ) x f = List.concat_map f x in
  let* { boxed_type = boxed_index
       ; tested_type = tested_index
       ; of_int
       ; unbox = unbox_index
       ; extra_bounds
       ; max_index
       }
    =
    indices
  in
  let* { boxed_type = boxed_data
       ; tested_type = tested_data
       ; width
       ; unboxed_sigil
       ; box = box_data
       ; unbox = unbox_data
       ; eq
       ; example
       }
    =
    datas
  in
  let setup =
    type_utilities_template
      ~boxed_index
      ~boxed_data
      ~generate_data:example
      ~to_index:of_int
      ~unbox_index
      ~unbox_data
      ~box_data
      ~data_equal:eq
      ~extra_bounds
      ~max_index
  in
  let tests =
    let set_sigil =
      if String.starts_with ~prefix:"i" width
      then String.sub width 1 (String.length width - 1)
      else width
    in
    List.map
      (fun { container; create; test_setters } ->
        one_test_template
          ~test_setters
          ~index:tested_index
          ~boxed_data
          ~tested_data
          ~index_sigil:("_indexed_by_" ^ tested_index)
          ~get_sigil:width
          ~set_sigil
          ~unboxed_sigil
          ~container
          ~create)
      containers
  in
  [ test_group_template ~setup ~tests ]
;;

template ~tests:(String.concat "" tests) |> print_string
